apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.github.rstockbridge.showstats"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 4
        versionName "2.0.2"
        buildConfigField "String", "SETLISTFM_KEY", setlistfmKey
    }

    signingConfigs {
        release {
            String releaseKeyStoreFileName = System.properties['user.home'] + "/keystores/showstats_upload.jks"

            if (file(releaseKeyStoreFileName).exists()) {
                storeFile file(releaseKeyStoreFileName)
                storePassword showstatsUploadPassword
                keyAlias showstatsUploadAlias
                keyPassword showstatsUploadPassword
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-debug.pro'
            applicationIdSuffix ".debug"

            buildConfigField "boolean", "ALLOW_FORCE_CRASH", "true"
            resValue "string", "app_name", "ShowStats Debug"
            resValue "string", "maps_api_key", "AIzaSyBGKejn03doMCQUJReYdKqpEyXRaKP5jo8"
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "ALLOW_FORCE_CRASH", "true"
            resValue "string", "app_name", "ShowStats"
            resValue "string", "maps_api_key", "AIzaSyCIBaQpFBNgpinWKxQS8IlGa1g4Uzou4BE"

            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

// this locks all support libraries to the same version to resolve version conflicts
configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'com.android.support'
                && !details.requested.name.contains('multidex') ) {
            details.useVersion "28.0.0"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'

    implementation 'com.google.android.material:material:1.0.0'

    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    implementation 'com.google.firebase:firebase-core:17.0.0'
    implementation 'com.google.firebase:firebase-auth:18.0.0'
    implementation 'com.google.firebase:firebase-firestore:20.1.0'
    implementation 'com.google.firebase:firebase-functions:18.0.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.0'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'


    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
